output "workspace" {
  description = "The resource group that all resources are associated with."
  value       = local.workspace
}

output "postgres" {
  description = "Connection info for Postgres."
  value       = module.postgres.rds
  sensitive   = true
}

output "redis" {
  description = "Connection information for Redis."
  value       = module.redis.elasticache
  sensitive   = true
}

output "kafka" {
  description = "Connection info for Kafka."
  value = var.managed_sync_enabled ? {
    cluster_bootstrap_brokers = module.kafka[0].cluster_bootstrap_brokers_sasl_scram
    cluster_username          = module.kafka[0].kafka_credentials.username
    cluster_password          = module.kafka[0].kafka_credentials.password
    cluster_mechanism         = module.kafka[0].kafka_credentials.mechanism
    cluster_tls_enabled       = module.kafka[0].cluster_tls_enabled
  } : {}
  sensitive = true
}

output "logs_bucket" {
  description = "The bucket used to store system logs."
  value       = module.storage.s3.logs_bucket
  sensitive   = true
}

output "auditlogs_bucket" {
  description = "The bucket used to store audit logs."
  value       = module.storage.s3.auditlogs_bucket
  sensitive   = true
}

output "minio" {
  description = "MinIO server connection info."
  value = {
    public_bucket       = module.storage.s3.public_bucket
    private_bucket      = module.storage.s3.private_bucket
    managed_sync_bucket = module.storage.s3.managed_sync_bucket
    microservice_user   = module.storage.s3.minio_microservice_user
    microservice_pass   = module.storage.s3.minio_microservice_pass
    root_user           = module.storage.s3.access_key_id
    root_password       = module.storage.s3.access_key_secret
  }
  sensitive = true
}

output "bastion" {
  description = "Bastion server connection info."
  value = {
    public_dns  = module.bastion.connection.bastion_dns
    private_key = module.bastion.connection.private_key
  }
  sensitive = true
}

output "cluster_name" {
  description = "The name of the EKS cluster."
  value       = module.cluster.eks_cluster.name
}

# ---------------------------------------------------------------------------
# ArgoCD outputs (only populated when argocd_enabled = true)
# ---------------------------------------------------------------------------

output "argocd_enabled" {
  description = "Whether ArgoCD is bootstrapped on this cluster."
  value       = var.argocd_enabled
}

output "argocd_namespace" {
  description = "The namespace ArgoCD is installed in."
  value       = var.argocd_enabled ? module.argocd[0].argocd_namespace : null
}

output "eso_role_arn" {
  description = "IAM role ARN used by the External Secrets Operator."
  value       = var.argocd_enabled ? module.argocd[0].eso_role_arn : null
}

output "secrets_manager_env_secret" {
  description = "Name of the Secrets Manager secret containing Paragon env config."
  value       = var.argocd_enabled && local.argocd_secrets_ready ? module.secrets[0].env_secret_name : null
}
