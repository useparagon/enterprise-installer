# credentials
variable "gcp_credential_json_file" {
  description = "The path to the GCP credential JSON file. All other `gcp_` variables are ignored if this is provided."
  type        = string
  default     = null
}

variable "gcp_project_id" {
  description = "The id of the Google Cloud Project. Required if not using `gcp_credential_json_file`."
  type        = string
  default     = null
}

variable "gcp_private_key_id" {
  description = "The id of the private key for the service account. Required if not using `gcp_credential_json_file`."
  type        = string
  sensitive   = true
  default     = null
}

variable "gcp_private_key" {
  description = "The private key for the service account. Required if not using `gcp_credential_json_file`."
  type        = string
  sensitive   = true
  default     = null
}

variable "gcp_client_email" {
  description = "The client email for the service account. Required if not using `gcp_credential_json_file`."
  type        = string
  sensitive   = true
  default     = null
}

variable "gcp_client_id" {
  description = "The client id for the service account. Required if not using `gcp_credential_json_file`."
  type        = string
  sensitive   = true
  default     = null
}

variable "gcp_client_x509_cert_url" {
  description = "The client certificate url for the service account. Required if not using `gcp_credential_json_file`."
  type        = string
  sensitive   = true
  default     = null
}

variable "gcp_assume_role" {
  description = "Whether to assume a role for the service account instead of using JSON credentials."
  type        = bool
  default     = false
}

# account
variable "organization" {
  description = "Name of organization to include in resource names."
  type        = string

  validation {
    condition     = length(var.organization) <= 16
    error_message = "The `organization` input must be 16 or less characters."
  }
}

variable "environment" {
  description = "Type of environment being deployed to."
  type        = string
  default     = "enterprise"
}

variable "vpc_cidr" {
  description = "CIDR for the virtual network. A `/16` (65,536 IPs) or larger is recommended."
  type        = string
  default     = "10.0.0.0/16"
}

variable "region" {
  description = "The region where to host Google Cloud Organization resources."
  type        = string
}

variable "region_zone" {
  description = "The zone in the region where to host Google Cloud Organization resources."
  type        = string
}

variable "region_zone_backup" {
  description = "The backup zone in the region where to host Google Cloud Organization resources."
  type        = string
}

# cloudflare
variable "cloudflare_api_token" {
  description = "Cloudflare API token created at https://dash.cloudflare.com/profile/api-tokens. Requires Edit permissions on Account `Cloudflare Tunnel`, `Access: Organizations, Identity Providers, and Groups`, `Access: Apps and Policies` and Zone `DNS`"
  type        = string
  sensitive   = true
  default     = "dummy-cloudflare-tokens-must-be-40-chars"
}

variable "cloudflare_tunnel_enabled" {
  description = "Flag whether to enable Cloudflare Zero Trust tunnel for bastion"
  type        = bool
  default     = false
}

variable "cloudflare_tunnel_subdomain" {
  description = "Subdomain under the Cloudflare Zone to create the tunnel"
  type        = string
  default     = ""
}

variable "cloudflare_tunnel_zone_id" {
  description = "Zone ID for Cloudflare domain"
  type        = string
  sensitive   = true
  default     = ""
}

variable "cloudflare_tunnel_account_id" {
  description = "Account ID for Cloudflare account"
  type        = string
  sensitive   = true
  default     = ""
}

variable "cloudflare_tunnel_email_domain" {
  description = "Email domain for Cloudflare access"
  type        = string
  sensitive   = true
  default     = "useparagon.com"
}

# optional network restrictions
variable "ssh_whitelist" {
  description = "An optional list of IP addresses to whitelist ssh access."
  type        = string
  default     = ""
}

variable "disable_deletion_protection" {
  description = "Used to disable deletion protection on database and storage resources."
  type        = bool
  default     = false
}

variable "auditlogs_retention_days" {
  description = "The number of days to retain audit logs before deletion."
  type        = number
  default     = 365
}

variable "auditlogs_lock_enabled" {
  description = "Whether to lock the GCS audit logs bucket retention policy."
  type        = bool
  default     = false
}

# postgres
variable "postgres_tier" {
  description = "The instance type to use for Postgres."
  type        = string
  default     = "db-custom-2-7680"
  # https://cloud.google.com/sql/docs/mysql/instance-settings#:~:text=see%20Instance%20Locations.-,Machine,-Type
}

variable "postgres_multiple_instances" {
  description = "Whether or not to create multiple Postgres instances. Used for higher volume installations."
  type        = bool
  default     = true
}

# redis
variable "redis_multiple_instances" {
  description = "Whether or not to create multiple Redis instances."
  type        = bool
  default     = true
}

variable "redis_memory_size" {
  description = "The size of the Redis instance (in GB)."
  type        = number
  default     = 2
}

# managed sync (GMK = Google Managed Kafka)
variable "managed_sync_enabled" {
  description = "Whether to enable managed sync (GMK cluster, managed_sync bucket, postgres and redis instances)."
  type        = bool
  default     = false
}

variable "gmk_kafka_version" {
  description = "Kafka version for the Google Managed Kafka cluster (version offered by the service)."
  type        = string
  default     = "3.7.1"
}

variable "gmk_vcpu_count" {
  description = "Number of vCPUs for the GMK cluster (minimum 3 in GCP)."
  type        = number
  default     = 3
}

variable "gmk_memory_gib" {
  description = "Memory in GiB for the GMK cluster (1-8 GiB per vCPU)."
  type        = number
  default     = 6
}

variable "gmk_disk_size_gib" {
  description = "Disk size in GiB per broker for the GMK cluster."
  type        = number
  default     = 100
}

variable "gmk_auto_rebalance" {
  description = "Whether to enable automatic partition rebalancing across brokers (can add load)."
  type        = bool
  default     = false
}

variable "gmk_sasl_mechanism" {
  description = "SASL mechanism: plain (module creates SA key and outputs in kafka.cluster_password) or oauthbearer (Workload Identity)."
  type        = string
  default     = "plain"

  validation {
    condition     = contains(["oauthbearer", "plain"], var.gmk_sasl_mechanism)
    error_message = "gmk_sasl_mechanism must be \"oauthbearer\" or \"plain\"."
  }
}

variable "gmk_sasl_plain_key_file_path" {
  description = "Optional path to your own Kafka SA key JSON for SASL/PLAIN. When empty, the module creates the key and outputs it in kafka.cluster_password."
  type        = string
  default     = ""
}

# kubernetes
variable "k8s_version" {
  description = "The version of Kubernetes to run in the cluster."
  type        = string
  default     = "1.33"
}

variable "k8s_min_node_count" {
  description = "Minimum number of node Kubernetes can scale down to."
  type        = number
  default     = 2
}

variable "k8s_max_node_count" {
  description = "Maximum number of node Kubernetes can scale up to."
  type        = number
  default     = 50
}

variable "k8s_spot_instance_percent" {
  description = "The percentage of spot instances to use for Kubernetes nodes."
  type        = number
  default     = 80
  validation {
    condition     = var.k8s_spot_instance_percent >= 0 && var.k8s_spot_instance_percent <= 100
    error_message = "Value must be between 0 - 100."
  }
}

variable "k8s_ondemand_node_instance_type" {
  description = "The compute instance type to use for Kubernetes on demand nodes."
  type        = string
  default     = "e2-standard-4"
}

variable "k8s_spot_node_instance_type" {
  description = "The compute instance type to use for Kubernetes spot nodes."
  type        = string
  default     = "e2-standard-4"
}

variable "k8s_disable_public_endpoint" {
  description = "Used to disable public endpoint on GKE cluster."
  type        = bool
  default     = true
}

variable "k8s_master_authorized_networks" {
  description = "List of CIDRs allowed to reach the GKE control plane (Master Authorized Networks). Use [{ cidr_block = \"0.0.0.0/0\", display_name = \"all\" }] to allow all IPs (e.g. from any country). Empty list = only cluster nodes (restricted)."
  type = list(object({
    cidr_block   = string
    display_name = optional(string, "")
  }))
  default = []
}

variable "use_storage_account_key" {
  description = "Whether to use the storage service account privatekey for the storage service account."
  type        = bool
  default     = false
}

variable "tfc_agent_token" {
  description = "Terraform Cloud Agent token to support Terraform runs from the bastion"
  type        = string
  sensitive   = true
  default     = ""
}

locals {
  creds_json     = try(jsondecode(file(var.gcp_credential_json_file)), {})
  gcp_project_id = try(local.creds_json.project_id, var.gcp_project_id)

  gcp_creds = jsonencode({
    type                        = "service_account",
    auth_provider_x509_cert_url = "https://www.googleapis.com/oauth2/v1/certs",
    auth_uri                    = "https://accounts.google.com/o/oauth2/auth",
    token_uri                   = "https://oauth2.googleapis.com/token",
    client_email                = try(local.creds_json.client_email, var.gcp_client_email),
    client_id                   = try(local.creds_json.client_id, var.gcp_client_id),
    client_x509_cert_url        = try(local.creds_json.client_x509_cert_url, var.gcp_client_x509_cert_url),
    gcp_project_id              = try(local.creds_json.gcp_project_id, var.gcp_project_id),
    private_key                 = try(local.creds_json.private_key, var.gcp_private_key),
    private_key_id              = try(local.creds_json.private_key_id, var.gcp_private_key_id),
  })

  # hash of project ID to help ensure uniqueness of resources like bucket names
  # coalesce so tflint/validate can run when gcp_project_id is not set (e.g. no tfvars)
  hash      = substr(sha256(coalesce(local.gcp_project_id, "tflint")), 0, 8)
  workspace = nonsensitive("paragon-${var.organization}-${local.hash}")

  default_labels = {
    name         = local.workspace
    environment  = var.environment
    organization = var.organization
    creator      = "terraform"
  }

  // get distinct values from comma-separated list, filter empty values and trim them
  ssh_whitelist = distinct([for value in split(",", var.ssh_whitelist) : "${trimspace(value)}${replace(value, "/", "") != value ? "" : "/32"}" if trimspace(value) != ""])
}
