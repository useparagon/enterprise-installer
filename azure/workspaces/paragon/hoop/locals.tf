locals {
  detected_cloud    = "azure"
  connection_prefix = coalesce(var.hoop_agent_name, var.organization)

  connection_environment = var.customer_facing ? "prod" : "staging"
  slack_enabled = (
    var.hoop_enabled &&
    try(var.hoop_slack_bot_token, null) != null && var.hoop_slack_bot_token != "" &&
    try(var.hoop_slack_app_token, null) != null && var.hoop_slack_app_token != "" &&
    length(var.hoop_slack_channel_ids) > 0
  )

  postgres_connections = try(var.infra_vars.postgres.value, null) != null ? {
    for db_schema, db_config in var.infra_vars.postgres.value :
    "postgres-${db_schema}" => {
      name    = length(keys(var.infra_vars.postgres.value)) == 1 ? "${local.connection_prefix}-postgres-db" : "${local.connection_prefix}-${db_schema}-db"
      type    = "database"
      subtype = "postgres"
      command = null
      secrets = {
        "envvar:HOST"    = db_config.host
        "envvar:PORT"    = tostring(db_config.port)
        "envvar:USER"    = db_config.user
        "envvar:PASS"    = db_config.password
        "envvar:DB"      = db_config.database
        "envvar:SSLMODE" = try(db_config.sslmode, "disable")
      }
      access_mode_runbooks = "enabled"
      access_mode_exec     = "enabled"
      access_mode_connect  = "disabled"
      access_schema        = "enabled"
      guardrail_rules      = var.hoop_postgres_guardrail_rules
      tags = {
        environment     = local.connection_environment
        customer_facing = var.customer_facing
        criticality     = "critical"
        access-level    = "private"
        impact          = "high"
        service-type    = "database"
        database-type   = "postgres"
        cloud           = local.detected_cloud
      }
    }
  } : {}

  legacy_redis = try(var.infra_vars.redis.value, null) != null ? var.infra_vars.redis.value : {}

  # During migration, output redis points at legacy and redis_managed at Azure Managed Redis.
  # Skip managed hoop entries when host matches legacy (post-cutover both outputs are the same).
  managed_redis_hoop_instances = try(var.infra_vars.redis_managed.value, null) != null ? {
    for name, cfg in var.infra_vars.redis_managed.value :
    name => cfg
    if !contains(keys(local.legacy_redis), name) || try(local.legacy_redis[name].host, "") != cfg.host
  } : {}

  redis_hoop_entries = merge(
    {
      for instance_name, instance_config in local.legacy_redis :
      "redis-${instance_name}" => {
        connection_name = "${local.connection_prefix}-redis-${instance_name}"
        config          = instance_config
      }
    },
    {
      for instance_name, instance_config in local.managed_redis_hoop_instances :
      "redis-managed-${instance_name}" => {
        connection_name = "${local.connection_prefix}-redis-managed-${instance_name}"
        config          = instance_config
      }
    },
  )

  redis_connection_tags = {
    environment     = local.connection_environment
    customer_facing = var.customer_facing
    criticality     = "critical"
    access-level    = "private"
    impact          = "high"
    service-type    = "cache"
    database-type   = "redis"
    cloud           = local.detected_cloud
  }

  # Unified connections map - combines all non-PostgreSQL connection types
  connections_merge = merge(
    # Redis: TLS when ssl=true; --cacert when ca_certificate is set; REDISCLI_AUTH for password.
    length(local.redis_hoop_entries) > 0 ? {
      for connection_key, entry in local.redis_hoop_entries :
      connection_key => {
        name    = entry.connection_name
        type    = "custom"
        subtype = "redis"
        command = concat(
          ["redis-cli", "-c", "-h", "$HOST", "-p", "$PORT", "-n", "$DB_NUMBER"],
          try(entry.config.ssl, false) ? ["--tls"] : [],
          try(entry.config.ssl, false) && try(entry.config.ca_certificate, null) != null && try(entry.config.ca_certificate, "") != "" ? ["--cacert", "$REDIS_CACERT_PATH"] : [],
        )
        secrets = merge(
          {
            "envvar:HOST"      = entry.config.host
            "envvar:PORT"      = tostring(entry.config.port)
            "envvar:DB_NUMBER" = tostring(try(entry.config.db_number, 0))
          },
          try(entry.config.ssl, false) && try(entry.config.ca_certificate, null) != null && try(entry.config.ca_certificate, "") != "" ? {
            "filesystem:REDIS_CACERT_PATH" = entry.config.ca_certificate
          } : {},
          try(entry.config.password, null) != null && try(entry.config.password, "") != "" ? {
            "envvar:REDISCLI_AUTH" = entry.config.password
          } : {},
        )
        access_mode_runbooks = "enabled"
        access_mode_exec     = "enabled"
        access_mode_connect  = "disabled"
        access_schema        = "disabled"
        guardrail_rules      = var.hoop_redis_guardrail_rules
        tags                 = local.redis_connection_tags
      }
    } : {},
    # Standard application connections
    # pgadmin
    try(var.infra_vars.postgres.value, null) != null ? {
      "pgadmin" = {
        name    = "${local.connection_prefix}-pgadmin"
        type    = "application"
        subtype = "tcp"
        command = ["bash"]
        secrets = {
          "envvar:HOST" = "pgadmin.paragon"
          "envvar:PORT" = "5050"
        }
        access_mode_runbooks = "enabled"
        access_mode_exec     = "enabled"
        access_mode_connect  = "enabled"
        access_schema        = "disabled"
        reviewers            = var.customer_facing ? var.reviewers_access_groups : null
        tags = {
          environment     = local.connection_environment
          customer_facing = var.customer_facing
          criticality     = "critical"
          access-level    = "private"
          impact          = "high"
          service-type    = "database"
          cloud           = local.detected_cloud
        }
      }
    } : {},
    # openobserve
    {
      "openobserve" = {
        name    = "${local.connection_prefix}-openobserve"
        type    = "application"
        subtype = "tcp"
        command = ["bash"]
        secrets = {
          "envvar:HOST" = "openobserve.paragon"
          "envvar:PORT" = "5080"
        }
        access_mode_runbooks = "enabled"
        access_mode_exec     = "enabled"
        access_mode_connect  = "enabled"
        access_schema        = "disabled"
        tags = {
          environment     = local.connection_environment
          customer_facing = var.customer_facing
          criticality     = "normal"
          access-level    = "private"
          impact          = "low"
          service-type    = "storage"
          cloud           = local.detected_cloud
        }
      }
    },
    # grafana (private monitoring UI; not exposed when listed in private_services)
    var.hoop_grafana_connection ? {
      "grafana" = {
        name    = "${local.connection_prefix}-grafana"
        type    = "application"
        subtype = "tcp"
        command = ["bash"]
        secrets = {
          "envvar:HOST" = "grafana.paragon"
          "envvar:PORT" = "4500"
        }
        access_mode_runbooks = "enabled"
        access_mode_exec     = "enabled"
        access_mode_connect  = "enabled"
        access_schema        = "disabled"
        tags = {
          environment     = local.connection_environment
          customer_facing = var.customer_facing
          criticality     = "normal"
          access-level    = "private"
          impact          = "low"
          service-type    = "monitoring"
          cloud           = local.detected_cloud
        }
      }
    } : {},
    # redis-insight
    try(var.infra_vars.redis.value, null) != null ? {
      "redis-insight" = {
        name    = "${local.connection_prefix}-redis-insight"
        type    = "application"
        subtype = "tcp"
        command = ["bash"]
        secrets = {
          "envvar:HOST" = "redis-insight.paragon"
          "envvar:PORT" = "8500"
        }
        access_mode_runbooks = "enabled"
        access_mode_exec     = "enabled"
        access_mode_connect  = "enabled"
        access_schema        = "disabled"
        reviewers            = var.customer_facing ? var.reviewers_access_groups : null
        tags = {
          environment     = local.connection_environment
          customer_facing = var.customer_facing
          criticality     = "critical"
          access-level    = "private"
          impact          = "high"
          service-type    = "database"
          cloud           = local.detected_cloud
        }
      }
    } : {},
    # K8s connections from tfvars (or default k8s-admin if none defined)
    length(var.k8s_connections) > 0 ? {
      for conn_name, conn_config in var.k8s_connections :
      "k8s-${conn_name}" => {
        name    = "${local.connection_prefix}-k8s-${conn_name}"
        type    = try(conn_config.type, "custom")
        subtype = try(conn_config.subtype, null) != null && try(conn_config.subtype, null) != "" ? conn_config.subtype : null
        command = try(conn_config.command, ["bash"])
        secrets = merge(
          {
            "envvar:REMOTE_URL"           = try(conn_config.remote_url, "https://kubernetes.default.svc.cluster.local")
            "envvar:INSECURE"             = try(conn_config.insecure, "true")
            "envvar:KUBECTL_NAMESPACE"    = try(conn_config.namespace, "paragon")
            "envvar:HEADER_AUTHORIZATION" = "Bearer ${try(data.kubernetes_secret.hoop_cluster_admin_token[0].data["token"], "")}"
          },
          try(conn_config.secrets, {})
        )
        access_mode_runbooks = try(conn_config.access_mode_runbooks, "enabled")
        access_mode_exec     = try(conn_config.access_mode_exec, "enabled")
        access_mode_connect  = try(conn_config.access_mode_connect, "enabled")
        access_schema        = try(conn_config.access_schema, "disabled")
        guardrail_rules      = try(conn_config.guardrail_rules, null) != null && length(try(conn_config.guardrail_rules, [])) > 0 ? conn_config.guardrail_rules : null
        reviewers            = try(conn_config.reviewers, null) != null && length(try(conn_config.reviewers, [])) > 0 ? conn_config.reviewers : null
        tags = merge({
          environment     = local.connection_environment
          customer_facing = var.customer_facing
          criticality     = "critical"
          access-level    = "private"
          impact          = "high"
          service-type    = "compute"
          cloud           = local.detected_cloud
          team            = "platform-eng"
        }, try(conn_config.tags, {}))
      }
      } : {
      # Default k8s-admin connection if no k8s_connections defined
      "k8s-admin" = {
        name    = "${local.connection_prefix}-k8s-admin"
        type    = "custom"
        subtype = null
        command = ["bash"]
        secrets = {
          "envvar:REMOTE_URL"           = "https://kubernetes.default.svc.cluster.local"
          "envvar:INSECURE"             = "true"
          "envvar:KUBECTL_NAMESPACE"    = "paragon"
          "envvar:HEADER_AUTHORIZATION" = "Bearer ${try(data.kubernetes_secret.hoop_cluster_admin_token[0].data["token"], "")}"
        }
        access_mode_runbooks = "enabled"
        access_mode_exec     = "enabled"
        access_mode_connect  = "enabled"
        access_schema        = "disabled"
        guardrail_rules      = null
        reviewers            = null
        tags = {
          environment     = local.connection_environment
          customer_facing = var.customer_facing
          criticality     = "critical"
          access-level    = "private"
          impact          = "high"
          service-type    = "compute"
          cloud           = local.detected_cloud
          team            = "platform-eng"
        }
      }
    },
    # Custom connections from tfvars
    try(var.custom_connections, {}) != {} ? {
      for conn_name, conn_config in var.custom_connections :
      "custom-${conn_name}" => {
        name                 = "${local.connection_prefix}-${conn_name}"
        type                 = conn_config.type
        subtype              = try(conn_config.subtype, null) != null && try(conn_config.subtype, null) != "" ? conn_config.subtype : null
        command              = try(conn_config.command, null)
        secrets              = conn_config.secrets
        access_mode_runbooks = try(conn_config.access_mode_runbooks, "enabled")
        access_mode_exec     = try(conn_config.access_mode_exec, "enabled")
        access_mode_connect  = try(conn_config.access_mode_connect, "disabled")
        access_schema        = try(conn_config.access_schema, "disabled")
        guardrail_rules      = try(conn_config.guardrail_rules, null) != null && length(try(conn_config.guardrail_rules, [])) > 0 ? conn_config.guardrail_rules : null
        reviewers            = try(conn_config.reviewers, null) != null && length(try(conn_config.reviewers, [])) > 0 ? conn_config.reviewers : null
        tags = merge({
          environment     = local.connection_environment
          customer_facing = var.customer_facing
          cloud           = try(conn_config.tags["cloud"], local.detected_cloud)
        }, try(conn_config.tags, {}))
      }
    } : {}
  )

  # Per-connection access control: k8s_connections can set access_control_groups; else use global customer_facing logic
  access_control_groups = {
    for conn_name, conn_config in local.all_connections :
    conn_name => (
      startswith(conn_name, "k8s-") && try(length(try(var.k8s_connections[replace(conn_name, "k8s-", "")].access_control_groups, [])), 0) > 0
      ? var.k8s_connections[replace(conn_name, "k8s-", "")].access_control_groups
      : (var.customer_facing ? var.restricted_access_groups : concat(var.restricted_access_groups, var.all_access_groups))
    )
  }

  postgres_access_control_groups = {
    for conn_name, conn_config in local.postgres_connections :
    conn_name => (
      var.customer_facing
      ? var.restricted_access_groups
      : concat(var.restricted_access_groups, var.all_access_groups)
    )
  }

  # Custom connections: use explicit access_control_groups, or default (restricted = oncall/admin; + all = dev-team-engineering when not customer_facing)
  custom_connections_access_control_groups = try(var.custom_connections, {}) != {} ? {
    for conn_name, conn_config in var.custom_connections :
    "custom-${conn_name}" => (
      try(length(conn_config.access_control_groups), 0) > 0
      ? conn_config.access_control_groups
      : (var.customer_facing ? var.restricted_access_groups : concat(var.restricted_access_groups, var.all_access_groups))
    )
  } : {}

  review_required_connections = {
    for conn_name, conn_config in local.all_connections :
    conn_name => conn_config
    if try(length(try(conn_config.reviewers, [])), 0) > 0
  }

  all_connections = local.connections_merge

  # Non-secret fields only (infra_vars is sensitive; keeps plan output readable)
  all_connections_config = {
    for k, v in local.all_connections : k => {
      name                 = nonsensitive(v.name)
      type                 = nonsensitive(v.type)
      subtype              = nonsensitive(v.subtype)
      command              = nonsensitive(v.command)
      access_mode_runbooks = nonsensitive(v.access_mode_runbooks)
      access_mode_exec     = nonsensitive(v.access_mode_exec)
      access_mode_connect  = nonsensitive(v.access_mode_connect)
      access_schema        = nonsensitive(v.access_schema)
      guardrail_rules      = try(v.guardrail_rules, null) != null && length(coalesce(try(v.guardrail_rules, null), [])) > 0 ? nonsensitive(v.guardrail_rules) : null
      reviewers            = try(v.reviewers, null) != null && length(coalesce(try(v.reviewers, null), [])) > 0 ? nonsensitive(v.reviewers) : null
      tags                 = nonsensitive(v.tags)
    }
  }

  postgres_connections_config = {
    for k, v in local.postgres_connections : k => {
      name                 = nonsensitive(v.name)
      type                 = nonsensitive(v.type)
      subtype              = nonsensitive(v.subtype)
      command              = nonsensitive(v.command)
      access_mode_runbooks = nonsensitive(v.access_mode_runbooks)
      access_mode_exec     = nonsensitive(v.access_mode_exec)
      access_mode_connect  = nonsensitive(v.access_mode_connect)
      access_schema        = nonsensitive(v.access_schema)
      guardrail_rules      = try(v.guardrail_rules, null) != null && length(coalesce(try(v.guardrail_rules, null), [])) > 0 ? nonsensitive(v.guardrail_rules) : null
      reviewers            = try(v.reviewers, null) != null && length(coalesce(try(v.reviewers, null), [])) > 0 ? nonsensitive(v.reviewers) : null
      tags                 = nonsensitive(v.tags)
    }
  }
}
