resource "azurerm_network_security_group" "postgres" {
  name                = "${var.workspace}-postgres"
  location            = var.resource_group.location
  resource_group_name = var.resource_group.name

  security_rule {
    name                       = "allow-private-postgres"
    priority                   = 100
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_address_prefix      = "VirtualNetwork"
    destination_port_range     = var.postgres_port
    source_port_range          = "*"
    destination_address_prefix = "*"
  }
}

resource "azurerm_subnet_network_security_group_association" "postgres" {
  subnet_id                 = var.private_subnet.id
  network_security_group_id = azurerm_network_security_group.postgres.id
}

locals {
  # Keep the DNS zone stable when Agent OS is toggled. Single-instance installs keep
  # the legacy Paragon server plus the optional dedicated Agent OS server in this map.
  postgres_dns_name = contains(keys(local.postgres_instances), "paragon") ? "${var.workspace}-dns.postgres.database.azure.com" : "${var.workspace}.postgres.database.azure.com"
}

resource "azurerm_private_dns_zone" "postgres" {
  name                = local.postgres_dns_name
  resource_group_name = var.resource_group.name

  depends_on = [azurerm_subnet_network_security_group_association.postgres]
}

resource "azurerm_private_dns_zone_virtual_network_link" "postgres" {
  name                  = "${var.workspace}-postgres"
  private_dns_zone_name = azurerm_private_dns_zone.postgres.name
  resource_group_name   = var.resource_group.name
  virtual_network_id    = var.virtual_network.id
}
