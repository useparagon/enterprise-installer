output "workspace" {
  description = "The resource group that all resources are associated with."
  value       = local.workspace
}

output "bastion" {
  description = "Bastion server connection info."
  value = {
    public_dns  = module.bastion.connection.bastion_dns
    private_key = module.bastion.connection.private_key
  }
  sensitive = true
}

output "postgres" {
  description = "Connection info for Postgres."
  value       = module.postgres.postgres
  sensitive   = true
}

output "logs_container" {
  description = "The bucket used to store system logs."
  value       = module.storage.blob.logs_container
  sensitive   = true
}

output "auditlogs_bucket" {
  description = "The bucket used to store audit logs."
  value       = module.storage.blob.auditlogs_container
  sensitive   = true
}

output "minio" {
  description = "MinIO server connection info."
  value = {
    public_bucket               = module.storage.blob.public_container
    public_storage_account_name = module.storage.blob.public_storage_account_name
    private_bucket              = module.storage.blob.private_container
    managed_sync_bucket         = module.storage.blob.managed_sync_container
    microservice_user           = module.storage.blob.minio_microservice_user
    microservice_pass           = module.storage.blob.minio_microservice_pass
    root_user                   = module.storage.blob.name
    root_password               = module.storage.blob.access_key
  }
  sensitive = true
}

output "redis" {
  description = "Primary Redis connection info for the paragon workspace. During migration (both modules enabled), returns legacy endpoints until redis_enabled is set to false."
  value       = var.redis_enabled ? module.redis.redis : module.redis_managed[0].redis
  sensitive   = true
}

output "redis_managed" {
  description = "Azure Managed Redis 7.4 endpoints (null when redis_managed_enabled is false). Use during migration for kubectl trial routing while output redis still points at legacy."
  value       = var.redis_managed_enabled ? module.redis_managed[0].redis : null
  sensitive   = true
}

output "redis_managed_export_storage" {
  description = "Blob storage for on-demand Azure Managed Redis RDB export (null when disabled or legacy Redis)."
  value       = var.redis_managed_enabled ? module.redis_managed[0].export_storage : null
}

output "cluster_name" {
  description = "The name of the AKS cluster."
  value       = module.cluster.kubernetes.name
}

output "resource_group" {
  description = "Resource Group that infrastructure was deployed to."
  value = {
    name     = module.network.resource_group.name
    location = module.network.resource_group.location
  }
}

output "kafka" {
  description = "Connection info for Kafka (Event Hubs for Kafka)."
  value = var.managed_sync_enabled ? {
    cluster_bootstrap_brokers = module.kafka[0].bootstrap_servers
    bootstrap_servers_private = module.kafka[0].bootstrap_servers_private
    namespace_name            = module.kafka[0].namespace_name
    cluster_username          = module.kafka[0].kafka_credentials.username
    cluster_password          = module.kafka[0].kafka_credentials.password
    cluster_mechanism         = module.kafka[0].kafka_credentials.mechanism
    cluster_tls_enabled       = module.kafka[0].tls_enabled
  } : null
  sensitive = true
}

output "argocd_namespace" {
  description = "Namespace where ArgoCD is installed."
  value       = var.argocd_enabled ? module.argocd[0].argocd_namespace : null
}

output "cluster_secret_store_name" {
  description = "Name of the ClusterSecretStore used by ESO."
  value       = var.argocd_enabled ? module.argocd[0].cluster_secret_store_name : null
}

output "key_vault_uri" {
  description = "URI of the Azure Key Vault holding GitOps secrets."
  value       = var.argocd_enabled ? module.argocd[0].key_vault_uri : null
}
